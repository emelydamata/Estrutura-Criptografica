{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "stainless-abraham",
   "metadata": {},
   "outputs": [],
   "source": [
    "/* ---------------------------------------------------------\n",
    "Generation of a Rainbow key pair over GF(q) \n",
    "public key is stored in public_key.txt\n",
    "private key is stored in private_key.txt\n",
    "------------------------------------------------------------*/\n",
    "\n",
    "clear ;\n",
    "\n",
    "q:=4; // field size\n",
    "parameters:=[3,2,2]; // Rainbow parameters (v_1, o_1, ..., o_u)\n",
    "u:= #parameters -1;\n",
    "\n",
    "v:=[];\n",
    "o:=[];\n",
    "\n",
    "v[1]:=parameters[1];\n",
    "for i:=1 to u do\n",
    "\to[i]:=parameters[i+1];\n",
    "\tv[i+1]:=v[i]+o[i];\n",
    "end for;\n",
    "\n",
    "n:=v[u+1];\n",
    "m:=n-v[1];\n",
    "GF<w>:=GaloisField(q);\n",
    "Vn:=VectorSpace(GF,n);\n",
    "Vm:=VectorSpace(GF,m);\n",
    "\n",
    "P<[x]>:=PolynomialRing(GF,n);\n",
    "Pol<[y]>:=PolynomialRing(P,n);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "connected-triple",
   "metadata": {},
   "outputs": [],
   "source": [
    "// key generation\n",
    "// affine map T -----------------------------------------------------------------------------\n",
    "repeat\n",
    "\tMT:=RandomMatrix(GF,n,n);\n",
    "until IsInvertible(MT) eq true;\n",
    "cT:= Random(Vn);\n",
    "\n",
    "T:=[];\n",
    "for i:=1 to n do\n",
    "\tT[i]:=P!0;\n",
    "\tfor j:=1 to n do\n",
    "\t\tT[i]:=T[i]+MT[i][j]*x[j];\n",
    "\tend for;\n",
    "\tT[i]:=T[i]+cT[i];\n",
    "end for;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "adaptive-watson",
   "metadata": {},
   "outputs": [],
   "source": [
    "// central map Q ---------------------------------------------------------------------------\n",
    "Qc:=[]; Q:=[];\n",
    "\n",
    "for greatloop:=1 to u do\n",
    "\n",
    "\tfor loop:=v[greatloop]-v[1]+1 to v[greatloop+1]-v[1] do // greatloop-th Layer --------------------------------\n",
    "\t\tQ[loop]:=Pol!0;\n",
    "\n",
    "\t\tfor i:=1 to v[greatloop] do\n",
    "\t\t\tfor j:=1 to v[greatloop+1] do\n",
    "\t\t\t\tQ[loop]:=Q[loop] + Random(GF)*Pol.i*Pol.j;\n",
    "\t\t\tend for;\n",
    "\t\tend for;\n",
    "\n",
    "\t\tfor i:=1 to v[greatloop+1] do\n",
    "\t\t\tQ[loop]:= Q[loop] + Random(GF)*Pol.i;\n",
    "\t\tend for;\n",
    "\n",
    "\t\tQ[loop]:=Q[loop]+Random(GF);\n",
    "\n",
    "\tend for;\n",
    "end for; "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "waiting-killer",
   "metadata": {},
   "outputs": [],
   "source": [
    "// affine map S ---------------------------------------------------------------------------------------\n",
    "\n",
    "repeat\n",
    "\tMSF:=RandomMatrix(GF,m,m);\n",
    "until IsInvertible(MSF) eq true;\n",
    "cS:=Matrix(Pol,m,1,ChangeUniverse(Eltseq(Random(Vm)),Pol));\n",
    "MS:=Matrix(Pol,m,m,ChangeUniverse(Eltseq(MSF),Pol));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "authorized-sewing",
   "metadata": {},
   "outputs": [],
   "source": [
    "// public key Pk ----------------------------------------------------------------------------\n",
    "QT:=ZeroMatrix(Pol,m,1);\n",
    "for i:=1 to m do\n",
    "\tQT[i][1]:=Evaluate(Q[i],y[1], T[1]);\n",
    "\tfor j:=2 to n do\n",
    "\t\tQT[i][1]:=Evaluate(QT[i][1],y[j], T[j]);\n",
    "\tend for;\n",
    "end for;\n",
    "\n",
    "Pk:=MS*QT+cS;\n",
    "\n",
    "D:=[];\n",
    "for i:=1 to m do\n",
    "\tD[i]:=MonomialCoefficient(Pk[i][1],1);\n",
    "end for;\n",
    "Pk := D;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "falling-consultancy",
   "metadata": {},
   "outputs": [],
   "source": [
    "// Output ----------------------------------------------------------------------------------\n",
    "printf \"************************************************* \\n\";\n",
    "printf \"*** Rainbow Signature Scheme - Key Generation *** \\n\";\n",
    "printf \"************************************************* \\n \\n\";\n",
    "\n",
    "printf\"What follows is the public_key and it should be written to the file public_key.txt\\n\";\n",
    "//SetOutputFile(\"public_key.txt\":Overwrite:=true);\n",
    "printf \"q:= %o ; \\n\", q;\n",
    "printf \"m:= %o ; \\n\", m;\n",
    "printf \"n:= %o ; \\n\", n;\n",
    "printf \"parameters:= %o ; \\n\", parameters;\n",
    "printf \"GF<w>:=GaloisField(q); \\n\";\n",
    "printf \"Pol<[x]>:=PolynomialRing(GF,n); \\n\";\n",
    "printf \"Pk:= %o ; \\n\\n\",D ;\n",
    "//UnsetOutputFile();\n",
    " \n",
    "printf\"What  follows is the private_key and it should be written to the file private_key.txt \\n\";\n",
    "//SetOutputFile(\"private_key.txt\":Overwrite:=true);\n",
    "printf \"q:= %o ; \\n\", q;\n",
    "printf \"v:= %o ; \\n\", v;\n",
    "printf \"n:= %o ; \\n\", n;\n",
    "printf \"m:= %o ; \\n\", m;\n",
    "printf \"GF<w>:=GaloisField(q); \\n\";\n",
    "printf \"Pol<[y]>:=PolynomialRing(GF,n); \\n\";\n",
    "printf \"Q:= %o ; \\n\", Q;\n",
    "printf \"MT:= %o ; \\n\", Eltseq(MT);\n",
    "printf \"cT:= %o ; \\n\", Eltseq(cT);\n",
    "printf \"MS:= %o ; \\n\", Eltseq(MS);\n",
    "printf \"cS:= %o ; \\n\", Eltseq(cS);\n",
    "//UnsetOutputFile();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "patient-pollution",
   "metadata": {},
   "outputs": [],
   "source": [
    "/* ---------------------------------------------------------\n",
    "reads in private key from private_key.txt\n",
    "generates Rainbow signature of a random hash value\n",
    "hash value and signature be stored in signature.txt\n",
    "but here passed on directly to last part of program\n",
    "------------------------------------------------------------*/\n",
    "\n",
    "printf \"******************************************************* \\n\";\n",
    "printf \"*** Rainbow Signature Scheme - Signature Generation *** \\n\";\n",
    "printf \"*** instead of      load \\\"private_key.txt\\\";         *** \\n\";\n",
    "printf \"*** a hashvalue is created below and signed          *** \\n\";      \n",
    "printf \"******************************************************* \\n \\n\";\n",
    "\n",
    "Hashspace:=VectorSpace(GF,m);\n",
    "Signspace:=VectorSpace(GF,n);\n",
    "\n",
    "MT:=Matrix(GF,n,n,Eltseq(MT));\n",
    "MS:=Matrix(GF,m,m,Eltseq(MS));\n",
    "cT:=Signspace!(Eltseq(cT));\n",
    "cS:=Hashspace!(Eltseq(cS));\n",
    "u:=#v-1;\n",
    "hashvalue:=Random(Hashspace);\n",
    "printf \"hashvalue= %o \\n \\n\", Eltseq(hashvalue);\n",
    "\n",
    "_,x,_:=IsConsistent(Transpose(MS), cS); // invert affine map S \n",
    "_,x,_:=IsConsistent(Transpose(MS), hashvalue- cS); // invert affine map S \n",
    "\n",
    "sequ:=[];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "surrounded-luther",
   "metadata": {},
   "outputs": [],
   "source": [
    "//invert central map Q ----------------------------------------------------------------------------\n",
    "repeat\n",
    "\tQ1:=Q;\n",
    "\tsequ:=[];\n",
    "// guessing Vinegar variables and simplification of the system\n",
    "\tfor i:= 1 to v[1] do\n",
    "\t\tsequ[i]:=Random(GF);\n",
    "\t\tfor loop:=1 to m do\n",
    "\t\t\tQ1[loop]:=Evaluate(Q1[loop],y[i],sequ[i]);\n",
    "\t\tend for;\n",
    "\tend for;\n",
    "\n",
    "\t//printf \"Vinegar Variables:= %o \\n \\n\", sequ;\n",
    " \n",
    "\tfor i:=2 to u+1 do\n",
    "\n",
    "\t\t// solving the linear systems\n",
    "\t\tOSpace:=VectorSpace(GF,v[i]-v[i-1]);\n",
    "\t\tQseq:=[]; c_Q1:=[];\n",
    "\t\tfor loop:=v[i-1]+1-v[1] to v[i]-v[1] do\n",
    "\t\t\tc_Q1:= c_Q1 cat [MonomialCoefficient(Q1[loop],1)];\n",
    "\t\t\tfor j:=v[i-1]+1 to v[i] do\n",
    "\t\t\t\tQseq:=Qseq cat [MonomialCoefficient(Q1[loop],y[j])];\n",
    "\t\t\tend for;\n",
    "\t\tend for;\n",
    "\t\tMatrixQ1:=Matrix(GF,v[i]-v[i-1],v[i]-v[i-1],Qseq);\n",
    "\t\tc_Q1:=OSpace!(c_Q1);\n",
    "\n",
    "\t\t// solution vector\n",
    "\t\tx1:=[];\n",
    "\t\tfor k:=1 to v[i]-v[i-1] do\n",
    "\t\t\tx1[k]:=x[k+v[i-1]-v[1]];\n",
    "\t\tend for;\n",
    "\n",
    "\t\ttr,y1,_:=IsConsistent(Transpose(MatrixQ1),OSpace!(x1)-c_Q1);\n",
    "\n",
    "\t\tif tr then\n",
    "\t\t\tprintf\"Solution for Layer %o: %o \\n\", i-1, y1;\n",
    "\t\telse\n",
    "\t\t\tprintf \"No Solution for Layer %o: Choose other Vinegar Variables\\n\", i-1;\n",
    "\t\t\tbreak;\n",
    "\t\tend if;\n",
    "\t\tsequ:=sequ cat Eltseq(y1);\n",
    "\n",
    "\n",
    "\t\t// simplify the system by substituting the values of the oil variables\n",
    "\t\tfor loop:=v[i]-v[1] to m do\n",
    "\t\t\tfor j:= v[i-1]+1 to v[i] do \n",
    "\t\t\t\tQ1[loop]:=Evaluate(Q1[loop],y[j],sequ[j]);\n",
    "\t\t\tend for;\n",
    "\t\tend for;\n",
    "\n",
    "\tend for;\n",
    "until tr;\n",
    "\n",
    "y:=Signspace!(sequ);\n",
    "\n",
    "_,signature,_:=IsConsistent(Transpose(MT),y-cT); // invert T "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "vocal-affiliation",
   "metadata": {},
   "outputs": [],
   "source": [
    "// Output\n",
    "signature := Eltseq(signature) ;\n",
    "printf\"signature:= %o \\n \\n\", signature;\n",
    "\n",
    "printf\"What follows should be written to the file signature.txt \\n \\n\";\n",
    "//SetOutputGFile(\"signature.txt\":Overwrite:=true);\n",
    "printf \"Hashspace:=VectorSpace(GF,m); \\n\";\n",
    "printf \"Signspace:= VectorSpace(GF,n);\\n\";\n",
    "printf \"hashvalue:=Hashspace!(%o) ; \\n\", Eltseq(hashvalue);\n",
    "printf \"signature:= Signspace!(%o) ; \\n\", Eltseq(signature);\n",
    "//UnsetOutputGFile();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "homeless-simon",
   "metadata": {},
   "outputs": [],
   "source": [
    "/* ---------------------------------------------------------------------------------------------\n",
    "verification of a Rainbow signature\n",
    "loads public key from public_key.txt and signature and hash value from signature_txt\n",
    "and checks if signature is a valid Rainbow signature for the hash value \n",
    "it is done here in line\n",
    " --------------------------------------------------------------------------*/\n",
    "\n",
    "printf \"********************************************************* \\n\";\n",
    "printf \"*** Rainbow Signature Scheme - Signature Verification *** \\n\";\n",
    "printf \"********************************************************* \\n \\n\";\n",
    "\n",
    "\n",
    "//load \"public_key.txt\";\n",
    "\n",
    "printf\"signature:= %o \\n\", signature;\n",
    "printf\"hashvalue:=    %o \\n\", Eltseq(hashvalue);\n",
    "\n",
    "hash2:= Evaluate( Pk,signature ) ;\n",
    "\n",
    "printf \"P(signature):= %o \\n \\n\", hash2;\n",
    "\n",
    "if Eltseq(hashvalue) eq hash2 then\n",
    "\tprintf \"CORRECT!\";\n",
    "else\n",
    "\tprintf \"FALSE!\";\n",
    "end if;\n",
    "\n",
    "exit;"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.2",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
